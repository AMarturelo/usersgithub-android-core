/* ------------------------ */
/* > Publish to Artifactory
/* ------------------------ */

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

def libraryGroupId = "com.amarturelo.usersgithub.android"
def libraryArtifactId = "core"
def libraryVersion = "1.0.1"

artifactoryPublish.skip = true
artifactoryPublish.dependsOn('build')

publishing {
    publications {
        aar(MavenPublication) {
            groupId = libraryGroupId
            artifactId = libraryArtifactId
            version = libraryVersion
            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
        }
    }
}

artifactoryPublish {
    publications(publishing.publications.aar)
}

artifactory {
    clientConfig.setIncludeEnvVars(true)
    clientConfig.info.addEnvironmentProperty('test.adding.dynVar',new Date().toString())

    contextUrl = artifactory_context_url
    publish {
        repository {
            repoKey = 'libs-release-local' // The Artifactory repository key to publish to
            username = "${artifactory_user}" // The publisher user name
            password = "${artifactory_password}" // The publisher password
            // This is an optional section for configuring Ivy publication (when publishIvy = true).
            ivy {
                ivyLayout = '[organization]/[module]/ivy-[revision].xml'
                artifactLayout = '[organization]/[module]/[revision]/[module]-[revision](-[classifier]).[ext]'
                mavenCompatible = true //Convert any dots in an [organization] layout value to path separators, similar to Maven's groupId-to-path conversion. True if not specified
            }
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publishArtifacts = true
            publishBuildInfo = true
            publishPom = true
            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'dev.team' : 'core']
        }
    }
}